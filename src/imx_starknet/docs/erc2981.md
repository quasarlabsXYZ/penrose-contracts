# ERC2981 - Royalties

This folder contains several implementations of a royalty standard on StarkNet for use with NFT contracts like ERC721 and ERC1155.

The current [EIP-2981 royalty standard](https://eips.ethereum.org/EIPS/eip-2981) is a fairly basic implementation of royalties for NFTs - one can define a single royalty recipient and percentage amount per token ID. Royalties are not enforced at the token contract level, and this standard simply lets the token contract signal a (currency agnostic) royalty amount given the sale price, and it is on the marketplace/exchange or whichever settlement layer the trade is being executed to enforce and distribute these royalties.

The design adheres to the EIP2981 standard for the most compatibility and familiarity in the ecosystem but explores some additional primitives that are designed to cover a range of different ways these royalties can be defined and managed.

## Contract structure

```
erc2981/
├── interfaces/
│   ├── IERC2981.cairo
│   ├── IERC2981_Flagged.cairo
│   ├── IERC2981_Immutable.cairo
│   ├── IERC2981_Mutable.cairo
│   └── IERC2981_Unidirectional_Mutable.cairo
├── flagged.cairo
├── immutable.cairo
├── mutable.cairo
└── unidirectional_mutable.cairo
```

- Each of the Cairo contracts in the root of the `erc2981` folder represents a different implementation of the EIP2981 standard. Choose any of them to import into an NFT contract based on your requirements and desired approach to royalties.
- In the recommended ERC721 preset `ERC721_Full.cairo`, the `Unidirectional Mutable` implementation is used as the recommended way to manage royalties.

## Contract Interface (ERC2981)

```
@contract_interface
namespace IERC2981:
    func royaltyInfo(tokenId : Uint256, salePrice : Uint256) -> (
            receiver : felt, royaltyAmount : Uint256):
    end
end
```

### ERC2981 Compatibility

- While there are several ways to approach the ERC2981 standard, this standard just requires that ERC2981-compliant contracts implement and expose the `royaltyInfo` function.
- Default royalty information (receiver and fee basis points) is exposed in an additional getter function for display purposes (e.g. a frontend marketplace may choose to display collection-wide royalty percentages in the collection details), but this getter should not be used to calculate the actual royalty amounts. `royaltyInfo` should always be used to get royalty amounts given a token ID and sale price, as per the EIP2981.
- The fee denominator is fixed at 10000 so that fee percentages are defined in basis points (1 = 0.01%, 10000 = 100%). Hence the fee percentage is referred to as `fee_basis_points` in the code for clarity.
- Since there are no existing standards for royalty modifier functions, notably the need for specific return values, all royalty modifier functions do not have a return value to avoid unnecessary `success` returns that are always TRUE. The functions will revert on any failures, so non-reverts imply a successful function execution.

## Presets

The royalty designs follow the standard set out in the [EIP-2981 royalty standard](https://eips.ethereum.org/EIPS/eip-2981), implementing `royaltyInfo`. [OpenZeppelin's Solidity implementation](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/common/ERC2981.sol) of the EIP2981 royalty standard was used as a reference when building the Cairo equivalents. This royalty implementation does not need to know about the method or reason for royalty payment to the recipient, including any notifications/events required in those flows. This responsibility is on the settlement contract to handle.

- `immutable.cairo` - fully immutable implementation of royalties, treating royalties as an immutable part of NFTs that should never be changed after initialization

- `mutable.cairo` - fully mutable implementation of royalties, giving the contract owner the most flexibility to change royalty details as needed

- `unidirectional_mutable.cairo` - an interesting foray into unidirectional mutability where the contract owner can only reduce royalty amounts, but not increase them. Royalty recipient addresses can still be changed without restrictions. This gives owners some flexibility to make changes while protecting NFT buyers and holders with the guarantee that their asset won't lose value overnight (due to royalty increases negatively impacting its saleability)

- `flagged.cairo` - an attempt to get the best of both worlds: fully mutable royalties upon initialization, and then only unidirectionally mutable after a flag is flipped in the contract, which cannot be unflipped. This implementation gives the most flexibility and holder protection, at the cost of some additional complexity

## Extensions

### Contract-wide vs token-specific royalties

Contract-wide royalties, also referred to as the default royalty, is a royalty applied to all tokens in the contract. Token-specific royalties refer to a royalty that is applied to one specific token ID out of the collection.

In most of the above royalty implementations, both contract-wide and token-specific royalties can be defined. The exception is in `immutable.cairo`, as it isn't feasible to define every token-specific royalty upon initialization and the royalties cannot be changed afterward.

Setting a token-specific royalty (where available) will override the default contract-wide royalty for that token ID. Not defining or resetting the token royalty will result in the default royalty being used.

### Multiple royalty recipients

See `finance/PaymentSplitter.cairo` for an implementation of a payment splitter contract intended to be used with these royalty standards, as per the example diagram below:

```
       ┌─────────────┐            ┌──────────────┐
       │ Marketplace ├────────────► NFT contract │
       └──────┬──────┘            └──────────────┘
              │                   Get royalty info
              │ Publish order
              │
        ┌─────▼─────┐
        │ Orderbook │
        └─────┬─────┘
              │
              │ Settle trade
              │
     ┌────────▼─────────┐
     │ Settlement layer │
     └────────┬─────────┘
              │
              │  Pay royalties
              │
     ┌────────▼──────────┐
     │ Payment Splitter  │
     └─────┬──────┬──────┘
           │      │
           │      │ Split payment
           │      │
 ┌─────────▼──┐ ┌─▼──────────┐
 │ Recipient1 │ │ Recipient2 │
 └────────────┘ └────────────┘
```

## Usage

To use these ERC2981 royalty implementations, import the base ERC2981 functions into your NFT contract and wrap them in the corresponding (view or external) functions with the relevant access controls. An example of such an integration can be found in `erc721/presets/ERC721_Full.cairo`, with some example snippets below:

```
@view
func royaltyInfo{syscall_ptr : felt*, pedersen_ptr : HashBuiltin*, range_check_ptr}(
        tokenId : Uint256, salePrice : Uint256) -> (receiver : felt, royaltyAmount : Uint256):
    let (exists) = ERC721_exists(tokenId)
    with_attr error_message("ERC721: token ID does not exist"):
        assert exists = TRUE
    end
    let (receiver : felt, royaltyAmount : Uint256) = ERC2981_UniDirectional_Mutable_royaltyInfo(tokenId, salePrice)
    return (receiver, royaltyAmount)
end

@external
func setTokenRoyalty{pedersen_ptr : HashBuiltin*, syscall_ptr : felt*, range_check_ptr}(
        tokenId : Uint256, receiver : felt, feeBasisPoints : felt):
    Ownable_only_owner()
    let (exists) = ERC721_exists(tokenId)
    with_attr error_message("ERC721: token ID does not exist"):
        assert exists = TRUE
    end
    ERC2981_UniDirectional_Mutable_setTokenRoyalty(tokenId, receiver, feeBasisPoints)
    return ()
end
```

Deploy an `Account` contract as a royalty recipient:

```typescript
// Deploy the OpenZeppelin Account contract
acc1 = await starknet.deployAccount("OpenZeppelin");
console.log("Deployed acc1 address: ", acc1.starknetContract.address);
```

Set a default (contract-wide) royalty:

```typescript
const royaltyRecipient = BigInt(acc1.starknetContract.address); // recipient address as felt
const royaltyFeeBasisPoints = 1000; // 1000 basis points = 10%

await owner.invoke(contract, "setDefaultRoyalty", {
  receiver: royaltyRecipient,
  feeBasisPoints: royaltyFeeBasisPoints,
});
```

Calculate the royalty amount for a given token ID and sale price:

```typescript
const tokenId = toUint256WithFelts(1); // tokenId 1 as Uint256 (with felts)
const salePrice = toUint256WithFelts(10); // sale price as Uint256 (with felts)

const royalty = await contract.call("royaltyInfo", {
  tokenId,
  salePrice,
});

console.log("Royalty recipient: ", royalty.receiver); // receiver = acc1.starknetContract.address
console.log("Royalty amount: ", royalty.royaltyAmount); // (10% of sale price) = 1
```

Since Cairo requires Uint256's `high` and `low` fields to be felts, we use a custom `toUint256WithFelts()` function to transform [`starknet.js`](https://github.com/0xs34n/starknet.js)'s Uint256 type:

```typescript
import { uint256 } from "starknet";

export type Uint256WithFelts = {
  low: BigInt;
  high: BigInt;
};

export function toUint256WithFelts(num: number.BigNumberish): Uint256WithFelts {
  const n = uint256.bnToUint256(num);
  return {
    low: BigInt(n.low.toString()),
    high: BigInt(n.high.toString()),
  };
}
```

## API Specification (ERC2981_UniDirectional_Mutable)

### Methods

```
func royaltyInfo(tokenId : Uint256, salePrice : Uint256) -> (
        receiver : felt, royaltyAmount : Uint256):
end

func getDefaultRoyalty() -> (receiver : felt, feeBasisPoints : felt):
end

func setDefaultRoyalty(receiver : felt, feeBasisPoints : felt):
end

func resetDefaultRoyalty():
end

func setTokenRoyalty(tokenId : Uint256, receiver : felt, feeBasisPoints : felt):
end

func resetTokenRoyalty(tokenId : Uint256):
end
```

#### `royaltyInfo`

Given `tokenId` and `salePrice`, calculate and return the corresponding `royaltyAmount` and royalty `receiver`, as per the [EIP2981 specification](https://eips.ethereum.org/EIPS/eip-2981).

Parameters:

```
tokenId: Uint256
salePrice: Uint256
```

Returns:

```
receiver: felt
royaltyAmount: Uint256
```

#### `getDefaultRoyalty`

Returns the default royalty `receiver` and `feeBasisPoints`. If no default royalty has been set, returns `0`, `0`. Note that this function should not be used to calculate the royalty amount and simply exposes default royalty info for display purposes. `royaltyInfo` should be used to calculate royalty fee amounts for orders as per EIP2981.

Parameters: None.

Returns:

```
receiver: felt
feeBasisPoints: felt
```

#### `setDefaultRoyalty`

Sets the default (contract-wide) royalty `receiver` and `feeBasisPoints`. Only callable by the contract owner. The new default royalty `feeBasisPoints` must be equal to or less than the existing default royalty `feeBasisPoints`.

Parameters:

```
receiver: felt
feeBasisPoints: felt
```

Returns: None.

#### `resetDefaultRoyalty`

Resets the default royalty `receiver` and `feeBasisPoints` to zero, which indicates no royalties. Only callable by the contract owner. Note this function will irreversibly set the default royalty fee to `0` as it can no longer be increased.

Parameters: None.

Returns: None.

#### `setTokenRoyalty`

Sets a token-specific royalty `receiver` and `feeBasisPoints` given a `tokenId`. Only callable by the contract owner. The new token royalty `feeBasisPoints` must be equal to or less than the existing token royalty `feeBasisPoints` for the given `tokenId`. In the case that no token royalty has been set for that `tokenId`, the new token royalty `feeBasisPoints` must be equal to or less than the default royalty `feeBasisPoints` instead.

Parameters:

```
tokenId: Uint256
receiver: felt
feeBasisPoints: felt
```

Returns: None.

#### `resetTokenRoyalty`

Resets the token-specific royalty `receiver` and `feeBasisPoints` to zero for a given `tokenId`. If there is a default royalty set, that will now be used instead. Only callable by the contract owner. Note that this function will irreversibly set the token royalty fee to `0` as it can no longer be increased.

Parameters:

```
tokenId: Uint256
```

Returns: None.
